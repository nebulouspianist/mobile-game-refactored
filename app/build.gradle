apply plugin: 'com.android.application'

ext {
    v = [
            cofoja: '1.3.1-20230406',
    ]

    cofoja = files("libs/cofoja-${v.cofoja}.jar")
}

android {
    namespace 'edu.ucsd.flappycow'
    compileSdkVersion 33

    defaultConfig {
        applicationId "edu.ucsd.flappycow"
        minSdkVersion 33
        targetSdkVersion 33
        versionCode 2023
        versionName "2023-Spring"

        javaCompileOptions {
            annotationProcessorOptions {
                argument "com.google.java.contract.extra_classpath", cofoja.asPath
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }

        // Run unit tests with Cofoja enabled.
        unitTests.all {
            jvmArgs "-ea", "-javaagent:./libs/cofoja-1.3.1-20230406.jar"
        }
    }
}

dependencies {
    // Core Dependencies
    // -----------------
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.browser:browser:1.5.0'
    implementation 'com.google.android.gms:play-services-ads:22.0.0'

    // Testing Dependencies
    // --------------------
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.12.4'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'org.robolectric:robolectric:4.9.2'

    // Cofoja Dependencies
    // -------------------

    // For implementation, we just need the annotation processor to work.
    implementation cofoja
    annotationProcessor cofoja

    // While running tests, we want to evaluate contracts, so we also need
    // ASM (and the test annotation processor as before).
    testImplementation 'org.ow2.asm:asm:9.4'
    testImplementation cofoja
    testAnnotationProcessor cofoja
}
